#define MAX_STEPS 150
#define MAX_DIST 150.0
#define SURF_DIST 0.0001
#define MIN_DIST 0.0
    
struct sColoredObj
{
    vec4 color;
    float dist;
};
        
// OBJintersect: Returns the intesection of two volumes
//    distA: the first volume
//    distA: the second volume
sColoredObj OBJintersect(sColoredObj distA, sColoredObj distB) 
{
    //return max(distA, distB);
    if (distA.dist > distB.dist)
        return distA;
    return distB;
}

// OBJintersect: Returns the union of two volumes
//    distA: the first volume
//    distA: the second volume
sColoredObj OBJunion(in sColoredObj distA, in sColoredObj distB) 
{
    //return min(distA, distB);
    if (distA.dist > distB.dist)
        return distB;
    return distA;

}

// OBJintersect: Returns the difference of two volumes
//    distA: the first volume
//    distA: the second volume, which will be removed
sColoredObj OBJdifference(in sColoredObj distA, in sColoredObj distB) 
{
    //return (distA.dist > -distB.dist) ? distA : distB; //WHY TF DOESNT ISNT THIS LEGAL
    if (distA.dist > -1.0 * distB.dist)
        return distA;
    distB.dist = -1.0 * distB.dist;
    return distB;
}

// createSphere: distance to a given sphere
//    center: centerpoint of sphere
//    radius: radius of sphere
//    p:      sample point
sColoredObj initSphere(vec4 center, float radius, 
                      vec4 color, vec3 p)
{
    sColoredObj sphere;
    sphere.color = color;
    sphere.dist = length(p - center.xyz) - radius;
    return sphere;
}

// planeDistance: distance to the plane y = 0
//    p: sample point 
sColoredObj initPlane(vec4 color, vec3 p)
{
	sColoredObj plane;
    plane.color = color;
    plane.dist = p.y;
    return plane;
}

sColoredObj initCylinder(vec3 position, vec3 top, vec3 bottom, vec4 center,
                          float radius, vec4 color)
{
	sColoredObj cylinder;
	cylinder.color = color;
    vec3  bottomToTop = bottom - top;
    vec3  positionToTop = (position - center.xyz) - top;
    float fullLength = dot(bottomToTop, bottomToTop);
    float currentLength = dot(positionToTop, bottomToTop);
    float x = length(positionToTop * fullLength - bottomToTop * currentLength) - radius * fullLength;
    float y = abs(currentLength - fullLength * 0.5) - fullLength * 0.5;
    float x2 = x * x;
    float y2 = y * y * fullLength;
    
    float d = (max(x, y) < 0.0) ? -min(x2, y2) : (((x > 0.0) ? x2 : 0.0) + ((y > 0.0) ? y2 : 0.0));
    
    cylinder.dist = sign(d) * sqrt(abs(d)) / fullLength;
	return cylinder;
}

sColoredObj initCone(vec3 position, vec3 top, vec3 bottom, vec4 center, float topRadius,
				float bottomRadius, vec4 color)
{
	sColoredObj cone;
	cone.color = color;
    float middleRadius  = bottomRadius - topRadius;
    float bottomFromTop = dot(bottom - top, bottom - top);
    float positionFromTop = dot((position - center.xyz) - top, (position - center.xyz) - top);
    float positionIntersect = dot((position - center.xyz) - top, bottom - top) / bottomFromTop;

    float x = sqrt( positionFromTop - positionIntersect * positionIntersect * bottomFromTop );

    float topx = max(0.0, x - ((positionIntersect < 0.5) ? topRadius : bottomRadius));
    float topy = abs(positionIntersect - 0.5) - 0.5;

    float k = middleRadius * middleRadius + bottomFromTop;
    float f = clamp( (middleRadius * (x - topRadius) + positionIntersect * bottomFromTop) / k, 0.0, 1.0 );

    float bottomx = x - topRadius - f * middleRadius;
    float bottomy = positionIntersect - f;
    
    float s = (bottomx < 0.0 && topy < 0.0) ? -1.0 : 1.0;
    
    cone.dist = s * sqrt( min(topx * topx + topy * topy * bottomFromTop,
                       bottomx * bottomx + bottomy * bottomy * bottomFromTop) );
	return cone;
}

sColoredObj initBox( vec3 position, vec3 dimensions, vec4 center, vec4 color)
{
	sColoredObj box;
	box.color = color;
    
	vec3 q = abs(position - center.xyz) - dimensions;
	box.dist = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
	return box;
}

sColoredObj initTorus( vec3 position, vec2 toroid, vec4 center, vec4 color)
{
    sColoredObj torus;
    torus.color = color;
    
    vec2 innerDistance = vec2(length((position.xy - center.xy)) - toroid.x,(position.z - center.z));
    torus.dist = length(innerDistance) - toroid.y;
    return torus;
}

sColoredObj initCapsule( vec3 position, vec3 top, vec3 bottom, vec4 center, 
                float radius, vec4 color)
{
    sColoredObj capsule;
    capsule.color = color;
    
    vec3 topFromPosition = (position - center.xyz) - top, topFromBottom = bottom - top;
    float height = clamp( dot(topFromPosition, topFromBottom)
                         / dot(topFromBottom, topFromBottom), 0.0, 1.0 );
    capsule.dist = length( topFromPosition - topFromBottom * height ) - radius;
    return capsule;
}

sColoredObj initEllipsoid( vec3 position, vec3 plane, vec4 center, vec4 color, int index )
{
    sColoredObj ellipsoid;
    ellipsoid.color = color;
    
    float section0 = length((position - center.xyz) / plane);
    float section1 = length((position - center.xyz) / (plane * plane));
    ellipsoid.dist = section0 * (section0 - 1.0) / section1;
    return ellipsoid;
}
    
float renderScene(vec3 position, out vec4 color)
{
    sColoredObj Spheres[10];
    sColoredObj Boxes[10];
    sColoredObj Cones[10];
    sColoredObj Cylinders[10];
    sColoredObj Tori[10];
    sColoredObj Capsules[10];
    sColoredObj Ellipsoids[10];
    
    // create scene objects
    sColoredObj planeDistance = initPlane(vec4(1.0),position);
    Spheres[0] = initSphere(vec4(-1.5, 1.0, 6.0, 1.0), 1.0, // Front wheel
                  vec4(1.0, 0.5, 0.0, 1.0), position);
    Spheres[1] = initSphere(vec4(-1.5, 1.0, 5.1, 1.0), 0.8, // Front wheel negative space left
                  vec4(1.0, 0.5, 0.0, 1.0), position);
    Spheres[2] = initSphere(vec4(-1.5, 1.0, 6.9, 1.0), 0.8, // Front wheel negative space right
                  vec4(1.0, 0.5, 0.0, 1.0), position);
    Spheres[3] = initSphere(vec4(-1.5, 1.0, 6.4, 1.0), 0.15, // Front wheel cap left
                  vec4(1.0), position);
    Spheres[4] = initSphere(vec4(-1.5, 1.0, 5.6, 0.0), 0.15, // Frot wheel cap right
                  vec4(1.0), position);
    Spheres[5] = initSphere(vec4(2.6, 1.0, 6.0,0.0), 0.15, // back wheel cap
                  vec4(1.0), position);

    Cylinders[0] = initCylinder(position, vec3(0.0, 0.0, 0.4), vec3(0.0, 0.0, -0.4), vec4(-1.5, 1.0, 6.0, 1.0),
                  0.9, vec4(0.0)); // Front wheel black
    Cylinders[1] = initCylinder(position, vec3(0.0, 0.0, 0.4), vec3(0.0, 0.0, -0.4), vec4(1.4, 0.65, 6.0, 1.0),
                  0.06, vec4(1.0, 0.5, 0.0, 1.0)); // foothold axel thing

    Cylinders[2] = initCylinder(position, vec3(0.0, 0.0, 0.4), vec3(0.0, 0.0, -0.4), vec4(2.6, 1.0, 6.0, 1.0),
                  0.7, vec4(1.0, 0.5, 0.0, 1.0)); //Back Wheel Negative Space
    Cylinders[3] = initCylinder(position, vec3(0.0, 0.0, 0.02), vec3(0.0, 0.0, -0.02), vec4(2.6, 1.0, 6.0, 1.0),
                  0.7, vec4(0.0)); //Back Wheel black


    Cylinders[4] = initCylinder(position, vec3(0.0, 0.0, 1.0), vec3(0.0, 0.0, -1.0), vec4(2.6, 1.0, 6.0, 1.0),
                  0.28, vec4(0.7)); //back wheel 'axis' negative
    Cylinders[5] = initCylinder(position, vec3(0.5,0.3,0.0), vec3(-1.2,-0.5,0.0), vec4(2.2, 1.6, 6.0, 1.0),
                  0.30, vec4(0.7)); //Back Wheel body cover
    Cylinders[6] = initCylinder(position, vec3(0.55,0.3,0.0), vec3(-1.25,-0.5,0.0), vec4(2.2, 1.6, 6.0, 1.0),
                  0.27, vec4(0.7)); //Back Wheel body cover negative
    
    Cylinders[7] = initCylinder(position, vec3(1.0,0.0,0.0), vec3(-2.5,0.0,0.0), vec4(1.2, 1.7, 6.0, 1.0),
                  0.5, vec4(1.0, 0.5, 0.0, 1.0)); // Main Body Top
    Cylinders[8] = initCylinder(position, vec3(2.0,-0.5,0.0), vec3(0.5,0.5,0.0), vec4(-2.0, 0.9, 6.0, 1.0),
                  0.7, vec4(1.0, 0.5, 0.0, 1.0)); // Main Body, lower footrest
    Cylinders[9] = initCylinder(position, vec3(0.55,0.5,0.0), vec3(-1.25,-0.5,0.0), vec4(2.8, 1.7, 6.0, 1.0),
                  0.53, vec4(1.0, 0.5, 0.0, 1.0)); //Back Wheel body cover negative

    
                                                            
    Cones[0] = initCone(position, vec3(0.0,0.0,0.0), vec3(1.4,0.0,0.0), vec4(-.7, 1.0, 6.0, 1.0),
                  0.6, 0.2, vec4(0.7)); // Front wheel cone
    Cones[1] = initCone(position, vec3(0.0,-0.1,0.0), vec3(-.5,0.0,0.0), vec4(0.95, 0.87, 5.8, 1.0),
                  0.2, 0.1, vec4(0.7)); // Left pipe bit
    Cones[2] = initCone(position, vec3(0.0,-0.1,0.0), vec3(-.5,0.0,0.0), vec4(0.95, 0.87, 6.2, 1.0),
                  0.2, 0.1, vec4(0.7)); // right pipe bit
    Cones[3] = initCone(position, vec3(0.0,0.0,-0.08), vec3(-.65,-0.15,0.0), vec4(2.65,1.7, 5.8, 1.0),
                  0.2, 0.1, vec4(0.7)); // Left exhaust
    Cones[4] = initCone(position, vec3(0.0,0.0,0.08), vec3(-.65,-0.15,0.0), vec4(2.65,1.7, 6.2, 1.0),
                  0.2, 0.1, vec4(0.7)); // right exhaust


	Boxes[0] = initBox(position, vec3(1.5, 0.65, 0.3), vec4(0.0, 1.0, 6.0, 1.0),
                  vec4(0.7)); // body
	Boxes[1] = initBox(position, vec3(1.0, 0.65, 0.02), vec4(1.6, 1.0, 5.72, 1.0),
                  vec4(0.7)); // backwheel frame left
	Boxes[2] = initBox(position, vec3(1.0, 0.65, 0.02), vec4(1.6, 1.0, 6.28, 1.0),
                  vec4(0.7)); // backwheel frame right
    Boxes[3] = initBox(position, vec3(0.5, 1.5, 0.5), vec4(3.1, 1.0, 6.0, 1.0),
                  vec4(0.7)); // backwheel cutoff negative
    Boxes[4] = initBox(position, vec3(0.7, 0.8, 0.25), vec4(2.8, 1.0, 6.0, 1.0),
                  vec4(0.7)); // backwheel cutoff negative
    Boxes[5] = initBox(position, vec3(0.7, 0.8, 1.25), vec4(2.2, 0.5, 6.0, 1.0),
                  vec4(1.0, 0.5, 0.0, 1.0)); // Main body back cutoff negative
    Boxes[6] = initBox(position, vec3(1.58, 0.5, 0.5), vec4(0.3, 1.25, 6.0, 1.0),
                  vec4(1.0, 0.5, 0.0, 1.0)); //Main Body
    Boxes[7] = initBox(position, vec3(1.3, 0.8, 1.25), vec4(-0.1, 0.3, 6.0, 1.0),
                  vec4(1.0, 0.5, 0.0, 1.0)); // Main body front cutoff negative
    Boxes[8] = initBox(position, vec3(1.3, 0.8, 1.25), vec4(-0.4, 0.96, 5.55, 1.0),
                  vec4(0));
    Boxes[9] = initBox(position, vec3(1.3, 0.8, 1.25), vec4(1.0, 1.5, 5.55, 1.0),
                  vec4(0));

	Tori[0] = initTorus(position, vec2(0.8,0.19), vec4(2.6, 1.0, 6.0, 1.0), vec4(1.0, 0.5, 0.0, 1.0));
    
    Capsules[0] = initCapsule(position, vec3(0.5, 0.0, 0.0), vec3(-0.5, 0.0, 0.0), vec4(-0.4, 1.75, 5.55, 1.0), 
                0.2, vec4(0.0)); //right window
    Capsules[1] = initCapsule(position, vec3(0.5, 0.0, 0.0), vec3(-0.5, 0.0, 0.0), vec4(-0.47, 1.75, 6.45, 1.0), 
                0.2, vec4(0.0)); //left window

    // use Logic to build the correct distance

    //front wheel done
    sColoredObj d = OBJdifference(Spheres[0],Spheres[1]);
    d = OBJdifference(d,Spheres[2]);
    d = OBJunion(d, Spheres[3]);
    d = OBJunion(d, Spheres[4]);
    d = OBJunion(d, Cylinders[0]);
    
    d = OBJunion(d, Boxes[0]);
    d = OBJunion(d, Cylinders[1]);
    d = OBJunion(d, Cones[0]);
    d = OBJunion(d, Cones[1]);
    d = OBJunion(d, Cones[2]);
	d = OBJunion(d, Boxes[1]);
    d = OBJunion(d, Boxes[2]);
    d = OBJunion(d, Cylinders[5]);
    d = OBJunion(d, Cones[3]);
    d = OBJunion(d, Cones[4]);

    
    d = OBJdifference(d, Cylinders[6]);
    d = OBJdifference(d, Cylinders[4]);
    d = OBJdifference(d, Boxes[3]);
    d = OBJdifference(d, Boxes[4]);
    
    sColoredObj body = OBJunion(Boxes[0], Cylinders[7]);
    body = OBJunion(body, Boxes[6]);
    body = OBJdifference(body, Boxes[5]);
    body = OBJdifference(body, Cylinders[8]);
    body = OBJdifference(body, Boxes[7]);
    body = OBJdifference(body, Cylinders[9]);
    
    sColoredObj windows = OBJunion(Spheres[5], Capsules[0]);
    windows = OBJunion(windows, Capsules[1]);
    windows = OBJdifference(windows, Boxes[8]);
    windows = OBJdifference(windows, Boxes[9]);


    
    sColoredObj wheel = OBJdifference(Tori[0],Cylinders[2]);
    wheel = OBJunion(wheel, Cylinders[3]);
    d = OBJunion(d, Spheres[5]);
    d = OBJunion(d, wheel);
    d = OBJunion(d, body);
    d = OBJunion(d, windows);

    
    d = OBJunion(d, planeDistance);
    
    /*Spheres[0] = initSphere(vec4(-1.5, 0.0, 6.0, 1.0), 1.0, 
                  vec4(1.0, 0.0, 0.0, 1.0), 0, position);
    Spheres[1] = initSphere(vec4(-1.0, 0.5, 5.5, 1.0),0.5,
             vec4(1.0, 1.0, 0.0, 1.0), 1, position);
    Spheres[2] = initSphere(vec4(1.5, 0.0, 6.0, 1.0), 1.0,
    				vec4(1.0, 0.0, 1.0, 1.0),2,position);
    Boxes[0] = initBox(position, vec3(0.7, 2.5, 0.6), vec4(0.0, 0.5, 6.5, 1.0),
                       vec4(1.0, 1.0, 0.0, 1.0), 0);
    Cones[0] = initCone(position, vec3(0.0,0.0,0.0), vec3(0.0,1.0,-0.1), vec4(0.0, 3.0, 6.6, 1.0),
                        0.7, 0.1, vec4(0.0, 0.5, 0.5, 1.0), 0);
    Cylinders[0] = initCylinder(position, vec3(0.0, -0.0, 0.0), vec3(-0.0, 1.0, 0.0), vec4(0.0, 1.0, 5.6, 1.0),
                          0.2, vec4(0.5, 0.2, 0.7, 1.0), 0);
    Tori[0] = initTorus(position, vec2(0.3, 0.3), vec4(3.0, 1.0, 5.6, 1.0), vec4(0.2, 0.9, 0.8, 1.0), 0);
    Capsules[0] = initCapsule(position, vec3(0.0, -0.0, 0.0), vec3(-0.0, 1.0, 0.0), vec4(-3.0, 1.0, 5.6, 1.0), 
                0.2, vec4(0.0, 1.0, 0.0, 1.0), 0);
    Ellipsoids[0] = initEllipsoid(position, vec3(0.2, 0.3, 0.4), vec4(-3.0, 3.0, 5.6, 1.0), 
                                  vec4(0.0, 0.0, 1.0, 1.0), 0);
    */
    //assign color
	color = d.color;
    
    return d.dist;
}

float shortestDistanceToSurface(out vec4 color, vec3 eye, vec3 marchingDirection, float start, float end) {
    //credit to https://www.shadertoy.com/view/4tcGDr
    
    float depth = start;
    for (int i = 0; i < MAX_STEPS; i++) {
        float dist = renderScene(eye + depth * marchingDirection, color);
        if (dist < SURF_DIST) {
			return depth;
        }
        depth += dist;
        if (depth >= end) {
            return end;
        }
    }
    return end;
}


vec3 getNormal(vec3 p){
    
    vec4 color;//placeholder, not needed for this.
    
    
    float d = renderScene(p,color);
    vec2 e = vec2(0.01, 0.0);
    
    vec3 n = d - vec3(
    renderScene(p - e.xyy,color),
    renderScene(p - e.yxy,color),
    renderScene(p - e.yyx,color));
    
    return normalize(n);
}

// calcLighting: calculates Lambertian and Blinn-Phong reflectance, and returns part of the summation of the final color
//    light: the current Point Light
//    position:     point on the sphere that we are drawing
//    normal:       normal vector from point
//    rayOrigin:    input ray origin
//	  viewVector:	vector from point to camera
vec4 calcLighting (in vec3 lightpos, in vec4 lightcolor, float lightintense, in vec3 position,
                   in vec3 normal, in vec3 rayOrigin, in vec3 viewVector, in vec4 Objectcolor)
{
    //Taken from Demetri's Lab 4
    // LAMBERTIAN REFLECTANCE
    vec3 lightVector = lightpos.xyz - position; // get vector of position to the light
	float lightLength = length(lightVector); // get length of light vector
    lightVector = lightVector / lightLength; // normalizes vector
   
    float diffuseCoefficient = max(0.0, dot(lightVector, normal)); // get coefficient
   
    float intensityRatio = lightLength/lightintense; // simplifying attenuation equation by doing this once
    float attenuation = 1.0 / (1.0 + intensityRatio +
                             (intensityRatio * intensityRatio)); // get attenuation
    float Lambertian = diffuseCoefficient * attenuation; // final lambertian

    // BLINN-PHONG REFLECTANCE
   
    float specular = max(0.0,dot(normal, normalize(viewVector + lightVector))); // get base specular coefficient
    specular *= specular; // specularCoefficient^2
    specular *= specular; // specularCoefficient^4
    specular *= specular * specular * specular; // ^16
    specular *= specular * specular * specular; // ^64
    specular *= specular * specular * specular; // ^256 = a
    specular *= 4.0; // Blinn coefficient
   
    //return (Lambertian * vec4(normal * 0.5 + 0.5, 1.0) + specular) * lightcolor;
    return (Lambertian * Objectcolor + specular) * lightcolor;
}